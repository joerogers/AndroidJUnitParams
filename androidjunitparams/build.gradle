import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id "com.android.library"
    id "com.vanniktech.maven.publish"
}

android {
    namespace = "com.forkingcode.androidjunitparams"
    compileSdkVersion = 30

    defaultConfig {
        minSdkVersion(16)
    }
    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro")
        }
    }
}

ext {
    groupId = "com.forkingcode.androidjunitparams"
    libraryName = "androidjunitparams"
    libraryDescription = "Android extentions to the JUnitParams library"
    libraryVersion = "1.2.0"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    api("androidx.test:runner:1.5.0")
    api("pl.pragmatists:JUnitParams:1.1.1")
}

mavenPublishing {
    // publishing to https://s01.oss.sonatype.org
    publishToMavenCentral(SonatypeHost.S01)

    signAllPublications()
    coordinates(groupId, libraryName, libraryVersion)

    // This generates POM.xml with proper parameters
    pom {
        packaging = "aar"

        // Add your description here
        name = libraryName
        description = libraryDescription
        url = "https://github.com/joerogers/AndroidJUnitParams"

        // Set your license
        licenses {
            license {
                name = "The Apache Software License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "joerogers"
                name = "Joe Rogers"
                url = "https://github.com/joerogers"
            }
        }
        scm {
            connection = "scm:git:git://github.com/joerogers/AndroidJUnitParams.git"
            developerConnection = "scm:git:git://github.com/joerogers/AndroidJUnitParams.git"
            url = "https://github.com/joerogers/AndroidJUnitParams"
        }
    }
}
